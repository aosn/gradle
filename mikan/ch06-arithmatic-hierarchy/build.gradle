// 共通の設定
subprojects {
  apply plugin: 'java'

  def defaultEncoding = 'UTF-8'
  compileJava.options.encoding = defaultEncoding

  sourceCompatibility = 1.7
  targetCompatibility = 1.7

  javadoc {
    options.links << 'http://docs.oracle.com/javase/jp/7/api/'
  }
}

// ライブラリプロジェクト
project(':arithmetic-lib') {
  repositories {
    jcenter()
  }
  dependencies {
    testCompile 'junit:junit:4.12'
  }
}

// アプリケーションプロジェクト
project(':arithmetic-main') {
  apply plugin: 'application'

  mainClassName = 'com.example.cli.SimpleCalc'
  applicationName = 'SimpleCalc'

  repositories {
    jcenter()
  }

  dependencies {
    compile project(':arithmetic-lib')
    testCompile 'junit:junit:4.12'
  }

  run {
    standardInput = System.in
  }
}
